plugins {
    id 'net.minecraftforge.gradle' version '5.1.72'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'wtf.gofancy.fancygradle' version '1.1.3-0'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

ext.configFile = file '../common/common-1.12.properties'

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.common = new ConfigSlurper().parse prop
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.blamejared.com' }
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://maven.neoforged.net/releases' }
    mavenCentral()
    gradlePluginPortal()
    maven { url = 'https://maven.gofancy.wtf/releases' }
}

version = "${common.mcversion}-${modversion}.${modsubversion}"

group = modgroup

archivesBaseName = modarchivename

if (gradle.startParameter.taskNames.any { it == 'jar' || it == 'build' }) {
    def propsFile = file('gradle.properties')
    def props = new Properties()
    propsFile.withInputStream { props.load(it) }
    def currentSub = Integer.parseInt(props['modsubversion'])
    def newSub = currentSub + 1
    modsubversion = newSub
    version = "${common.mcversion}-${modversion}.${modsubversion}"
    jar.doLast {
        props['modsubversion'] = newSub.toString()
        propsFile.withWriter { writer ->
            props.store(writer, null)
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${common.mcversion}-${common.forgeversion}"

    implementation fg.deobf("${common.mjei_version}")
    compileOnly fg.deobf("curse.maven:${common.mtop_version}")
    compileOnly fg.deobf("curse.maven:${common.hwya_version}")
    implementation fg.deobf("curse.maven:${common.tcon_version}")
    implementation fg.deobf("curse.maven:${common.mant_version}")

    compileOnly 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    runtimeOnly 'org.spongepowered:mixin:0.7.11-SNAPSHOT'

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

minecraft {
    mappings channel: 'stable', version: "${common.mcp_mappings}"
    runs {
        client {
            workingDirectory project.file('run')
            args '--width', '1920'
            args '--height', '1080'
            args '--add-opens=java.base/java.lang=ALL-UNNAMED'
            args '--version', common.mcversion
            args '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker'
            args '--mixin', 'mixins.smelteryio.json'
            args '--username', 'tgstyle'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${archivesBaseName}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run/server')
            args '--version', common.mcversion
            args '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker'
            args '--mixin', 'mixins.smelteryio.json'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${archivesBaseName}" {
                    source sourceSets.main
                }
            }
        }
    }
}

tasks.withType(JavaExec).configureEach { task ->
    if (task.name in ['runClient', 'runServer']) {
        doFirst {
            def modJar = project.file("build/libs/${archivesBaseName}-${version}.jar")
            if (modJar.exists()) {
                println "Removing JAR from classpath: ${modJar}"
                def newClasspath = classpath.minus(modJar).plus(sourceSets.main.output)
                classpath.setFrom(newClasspath)
            }
            println "Classpath files: ${classpath.files.collect { it.absolutePath }.join(', ')}"
        }
    }
}

compileJava.doLast {
    ['smelteryio'].each { name ->
        def refmapSrc = file("build/tmp/compileJava/compileJava-${name}-refmap.json")
        if (refmapSrc.exists()) {
            new File(sourceSets.main.output.resourcesDir, "mixins.${name}.refmap.json").bytes = refmapSrc.bytes
        }
    }
}

processResources {
    inputs.property 'version', "${modversion}.${modsubversion}"
    inputs.property 'mcversion', common.mcversion
    filesMatching('mcmod.info') {
        expand 'version': "${modversion}.${modsubversion}", 'mcversion': common.mcversion
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

mixin {
    config 'mixins.smelteryio.json'
    add sourceSets.main, 'mixins.smelteryio.refmap.json'
}

fancyGradle {
    patches {
        resources
        asm
        coremods
        codeChickenLib
        mergetool
    }
}

jar {
    manifest {
        attributes(
            'FMLCorePlugin': 'mctmods.smelteryio.mixin.MixinLoader',
            'FMLCorePluginContainsFMLMod': 'true'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Exclude all mixin-related content from the release jar *NOT LICENSED FOR DISTRO IN THIS MOD*
    exclude 'mctmods/smelteryio/mixin/**'
    exclude 'mixins.smelteryio.json'
    exclude 'mixins.smelteryio.refmap.json'
}

sourceSets.configureEach { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }